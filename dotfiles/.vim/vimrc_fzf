let g:fzf_layout = { 'window': { 'width': 0.88, 'height': 0.88 } }

" search
" let g:ag_prg='ag -S --nocolor --nogroup --column '
command! -bang -nargs=* AgWithExt call fzf#vim#ag_raw(<q-args>, fzf#vim#with_preview(), <bang>0)
fu! SearchWithAg(text, extension)
  let stxt = a:text
  if stxt == ''
    let stxt = expand('<cword>')
  endif
  if(a:extension == '')
    execute 'Ag ' . stxt . ''
  else
    execute 'AgWithExt -G "^.*\.' . a:extension. '$" ' . stxt . ''
  end
endf
nmap <Leader>ss :call SearchWithAg(GetInput("search: to search word under cursor, RETURN"), GetInput("file type:")) <CR>
vmap <Leader>ss :call SearchWithAg(getline('.')[getpos("'<")[2]-1:getpos("'>")[2]-1], GetInput("file type:")) <CR>

fu! SearchInBuffer(text)
    execute 'Lines ' . a:text
endf
nmap <Leader>sb :call SearchInBuffer(GetInput("search:")) <CR>

fu! MakeTags() "rebuild tags
  exe "!ctags -R -o newtags; mv newtags tags"
endf
nmap <Leader>mtg :call MakeTags()<CR>

" fzf
nmap <Leader>og :Tags<CR>
" nmap <Leader>oo :BTags<CR>
nmap <Leader>of :Files<CR>
nmap <Leader>ob :Buffers<CR>
nmap <Leader>oh :History<CR>
" nmap <Leader>oc :Commands<CR>
nmap <Leader>om :Marks<CR>
nmap <Leader>olc :Commits<SPACE>
nmap <Leader>olb :BCommits<CR>
command! -bar -bang -nargs=* -range=% -complete=customlist,fugitive#CompleteObject Commits let b:fzf_winview = winsaveview() | <line1>,<line2>call fzf#vim#commits(<q-args>, fzf#vim#with_preview({ "placeholder": "" }), <bang>0)

function GoTo(jumpline)
  let values = split(a:jumpline, ":")
  execute "e ".values[0]
  call cursor(str2nr(values[1]), str2nr(values[2]))
  execute "normal zvzz"
endfunction

function! Jumps()
  " Get jumps with filename added
  let jumps = map(reverse(copy(getjumplist()[0])), { key, val -> extend(val, {'name': getbufinfo(val.bufnr)[0].name }) })
  " let jumps = map(filter(reverse(copy(getjumplist()[0])), {key, val -> val.filename =~ "__Tagbar__"}), { key, val -> extend(val, {'name': getbufinfo(val.bufnr)[0].name }) })
  let jumptext = map(copy(jumps), { index, val -> (val.name).':'.(val.lnum).':'.(val.col+1).': '.GetLine(val.bufnr, val.lnum) })
  call fzf#run(fzf#vim#with_preview(fzf#wrap({ 'source': jumptext, 'column': 1, 'options': ['--delimiter', ':', '--bind', 'alt-a:select-all,alt-d:deselect-all', '--preview-window', '+{2}-/2'], 'sink': function('GoTo')})))
endfunction
command! Jumps call Jumps()
nmap <Leader>oj :call Jumps()<CR>

function! Changes()
  let changes  = reverse(copy(getchangelist()[0]))
  let changetext = map(copy(changes), { index, val -> expand('%').':'.(val.lnum).':'.(val.col+1).': '.GetLine(bufnr('%'), val.lnum) })
  call fzf#run(fzf#vim#with_preview(fzf#wrap({ 'source': changetext, 'column': 1, 'options': ['--delimiter', ':', '--bind', 'alt-a:select-all,alt-d:deselect-all', '--preview-window', '+{2}-/2'], 'sink': function('GoTo')})))
endfunction
command! Changes call Changes()
nmap <Leader>oc :call Changes()<CR>

